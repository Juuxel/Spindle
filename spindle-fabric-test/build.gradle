plugins {
    id 'fabric-loom'
}

configurations {
    boot {
        canBeResolved = true
        canBeConsumed = false

        extendsFrom loaderLibraries
        extendsFrom loomDevelopmentDependencies

        resolutionStrategy {
            force minecraftRuntimeLibraries.allDependencies
        }

        attributes {
            attribute Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, Usage.JAVA_RUNTIME)
        }
    }

    modulePath {
        canBeResolved = true
        canBeConsumed = false

        resolutionStrategy {
            force loaderLibraries.allDependencies
            force minecraftRuntimeLibraries.allDependencies
        }
    }

    runtimeClasspath {
        extendsFrom boot
    }
}

repositories {
    mavenLocal()
}

dependencies {
    minecraft 'net.minecraft:minecraft:1.19.4'
    mappings 'net.fabricmc:yarn:1.19.4+build.2:v2'

    modImplementation libs.fabric.loader
    boot libs.fabric.loader
    boot project(path: ':spindle', configuration: 'dev')
    boot libs.bundles.forge.environment
    boot 'net.minecrell:terminalconsoleappender'
    boot 'com.mojang:logging'

    runtimeOnly libs.bootstraplauncher
    modulePath libs.bootstraplauncher
    modulePath libs.bundles.asm
}

def resolvePaths(Configuration configuration) {
    return configuration.resolve().collect { it.absolutePath }
}

def setupClasspathData() {
    def modulePath = resolvePaths(configurations.modulePath).join(File.pathSeparator)
    def classpathFile = new File(buildDir, 'bootstrap_classpath.txt')
    classpathFile.text = resolvePaths(configurations.boot).join('\n')

    return [
        modulePath   : modulePath,
        classpathFile: classpathFile
    ]
}

afterEvaluate {
    def classpathData = setupClasspathData()

    loom {
        runs {
            spindleClient {
                client()
                configName = 'Spindle Client'
                mainClass.set 'cpw.mods.bootstraplauncher.BootstrapLauncher'
                ideConfigGenerated = true

                programArgs '--launchTarget', 'spindleclient'
                vmArgs '-p', classpathData.modulePath,
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--add-opens', 'java.base/java.util.jar=cpw.mods.securejarhandler',
                    '--add-opens', 'java.base/java.lang.invoke=cpw.mods.securejarhandler',
                    '--add-exports', 'java.base/sun.security.util=cpw.mods.securejarhandler'
                property 'legacyClassPath.file', classpathData.classpathFile.absolutePath
            }
        }
    }
}
