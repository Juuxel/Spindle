plugins {
    id("net.kyori.indra.licenser.spotless") version "3.0.1" apply false
    id("fabric-loom") version "1.2.+" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'net.kyori.indra.licenser.spotless'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17

        withSourcesJar()
    }

    group 'io.github.juuxel'
    version '0.0.20'

    repositories {
        mavenCentral()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net'
        }
        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net'
        }
        maven { url 'https://libraries.minecraft.net' }
    }

    compileJava {
        options.javaModuleVersion = project.version
    }

    jar {
        reproducibleFileOrder = true
        preserveFileTimestamps = false
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    indraSpotlessLicenser {
        licenseHeaderFile(rootProject.file("HEADER.txt"))
        newLine(true)
    }
}

