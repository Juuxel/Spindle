dependencies {
    implementation libs.bundles.forge.environment
}

jar {
    archiveClassifier = 'slim'
}

def bundleJar = tasks.register('bundleJar', Jar) {
    from sourceSets.main.output
    from(provider {
        def configuration = project(':spindle').configurations.fabricLoaderDependencies.copy()
        for (def group in ['org.ow2.asm', 'com.google.code.gson', 'com.google.guava']) {
            configuration.exclude group: group
        }
        for (def module in ['sponge-mixin']) {
            configuration.exclude module: module
        }
        files(
            configuration,
            project(':spindle').configurations.fabricLoader
        )
    }) {
        into 'META-INF/jars'
    }
    from(project(':spindle').tasks.named('jar')) {
        into 'META-INF/jars'
    }
}

assemble {
    dependsOn bundleJar
}

publishing {
    publications {
        maven {
            artifact bundleJar
        }
    }
}
